#define DEBUG 0

///////
// 文字列で表現した非負整数のBCD計算
///////

#define VAL(a) ((a) - '0')

// 先行する 0 を除去する
String iNorm(String a) {
  while(a.length() > 1) {
    if(a.charAt(0) != '0') {
      break;
    }
    a = a.substring(1);
  }
  return a;
}

// 値の大小比較
int iComp(String a, String b) {
  a = iNorm(a);
  b = iNorm(b);
  int sub = (int)a.length() - (int)b.length();
  if(sub != 0) {
    return sub;
  }
  return a.compareTo(b);
}

// 非負の整数の和を計算
String iAdd(String a, String b) {
  int len = (int)a.length() - (int)b.length();
  int i, sum, carry = 0;
  String c = "";
  
  if(len > 0) { // 文字列の長さを揃える
    for(i = 0; i < len; i++) {
      b = "0" + b;
    }
  }
  else if(len < 0) {
    for(i = 0; i < -len; i++) {
      a = "0" + a;
    }
  }
  len = a.length();
  for(i = len-1; i >= 0; i--) {
    int sum = VAL(a.charAt(i)) + VAL(b.charAt(i)) + carry;
    if(sum > 9) {
      sum -= 10;
      carry = 1; // 繰り上がり
    }
    else {
      carry = 0;
    }
    c = String(sum) + c;
  }
  if(carry)
    c = "1" + c;
  return iNorm(c);
}

// 差の計算 a >= b でなければならない
String iSub(String a, String b) {
  int len = (int)a.length() - (int)b.length();
  int i, sub, borrow = 0;
  String c = "";
  
  if(len > 0) { // 文字列の長さを揃える
    for(i = 0; i < len; i++) {
      b = "0" + b;
    }
  }
  else if(len < 0) {
    for(i = 0; i < -len; i++) {
      a = "0" + a;
    }
  }
  len = a.length();
  for(i = len-1; i >= 0; i--) {
    int sub = VAL(a.charAt(i)) - VAL(b.charAt(i)) - borrow;
    if(sub < 0) {
      sub += 10;
      borrow = 1; // 繰り下がり
    }
    else {
      borrow = 0;
    }
    c = String(sub) + c;
  }
  return iNorm(c);
}

// 積の計算
String iMul(String a, String b) {
  String c, amul;
  int i, j;

  c = "0";
  amul = a;
  for(j = b.length() - 1; j >= 0; j--) {
    for(i = 0; i < VAL(b.charAt(j)); i++) {
      c = iAdd(c, amul);
    }
    amul = amul + "0"; // 左シフト
  }
  return iNorm(c);
}

// 整数の除算
String rem; // 剰余を格納
String iDiv(String a, String b) {
  String mult;
  String x, ans = "0";
  int i, j;

  if(iComp(b, "0") == 0) {
    return "0";
  }
  rem = "";
  while(a.endsWith("0") && b.endsWith("0")) { // 計算時間とスタックの節約
    a = a.substring(0, a.length() - 1);
    b = b.substring(0, b.length() - 1);
    rem += "0";
  }
  x = b;
  for(i = 0; ; i++) {
    x = x + "0";
    if(iComp(a, x) < 0) {
      break;
    }
  }
  for(; i >= 0; i--) {
    if(iComp(a, "0") == 0) {
      break;
    }
    mult = "1";
    x = b;
    for(j = 0; j < i; j++) {
      mult = mult + "0";
      x = x + "0";
    }
    for(j = 0; j < 10; j++) {
      if(iComp(a, x) >= 0) {
        a = iSub(a, x);
        ans = iAdd(ans, mult);
      }
      else {
        break;
      }
    }
  }
  rem = iNorm(a + rem);
  return iNorm(ans);
}

// 剰余を読み出す関数 iDiv の実行後似呼び出し
String iRem() {
  return rem;
}

// 最大公約数の計算（互除法） a, b は 0 不可
String iGCD(String a, String b) {
  String c, r;

  if(iComp(a, b) < 0) {
    c = b;
    b = a;
    a = c;
  }
  for(;;) {
    iDiv(a, b);
    r = iRem();
    if(iComp(r, "0") == 0) {
      return b;
    }
    a = b;
    b = r;
  }
}
 
///////
// 分数計算部
///////

// 分数の構造体定義　分母分子の非負整数と符号で構成
struct FRAC {
  String u, b;
  char sign;
};

// 小数を表す文字列を分数に変換
struct FRAC str2FRAC(String s) {
  struct FRAC a;
  int ptPos, mul, i;

  if(s.length() == 0) {
    s = "0";
  }
  a.sign = 1;
  if(s.charAt(0) == '-') {
    a.sign = -1;
    s = s.substring(1);
  }
  ptPos = s.indexOf(".");
  if(ptPos >= 0) {
    int mul = (int)s.length() - ptPos - 1;
    a.u = s.substring(0, ptPos) + s.substring(ptPos + 1);
    if(a.u.length() == 0) {
      a.u = "0";
    }
    a.b = "1";
    for(i = 0; i < mul; i++) {
      a.b = a.b + "0";
    }
  }
  else {
    a.u = s;
    a.b = "1";
  }
  return a;
}

// 符号の反転
struct FRAC fSign(struct FRAC a) {
  a.sign = -a.sign;
  return a;
}

// 約分
struct FRAC fReduce(struct FRAC a) {
  String gcd;

  if(iComp(a.u, "0") == 0 || iComp(a.b, "0") == 0) {
    return a;
  }
  gcd = iGCD(a.u, a.b);
  if(iComp(gcd, "1") == 0) {
    return a;
  }
  a.u = iDiv(a.u, gcd);
  a.b = iDiv(a.b, gcd);
  return a;
}

// 和
struct FRAC fAdd(struct FRAC a, struct FRAC b) {
  struct FRAC c;

  c.b = iMul(a.b, b.b);
  a.u = iMul(a.u, b.b);
  b.u = iMul(b.u, a.b);
  if(a.sign == b.sign) {
    c.u = iAdd(a.u, b.u);
    c.sign = a.sign;
  }
  else {
    if(iComp(a.u, b.u) > 0) {
      c.u = iSub(a.u, b.u);
      c.sign = a.sign;
    }
    else {
      c.u = iSub(b.u, a.u);
      c.sign = b.sign;
    }
  }
  return fReduce(c);
}

// 差
struct FRAC fSub(struct FRAC a, struct FRAC b) {
  return fReduce(fAdd(a, fSign(b)));
}

// 積
struct FRAC fMul(struct FRAC a, struct FRAC b) {
  struct FRAC c;

  c.sign = a.sign * b.sign;
  c.u = iMul(a.u, b.u);
  if(iComp(c.u, "0") == 0) {
    c.b = 1;
    c.sign = 1;
    return c;
  }
  c.b = iMul(a.b, b.b);
  return fReduce(c);
}

// 商
struct FRAC fDiv(struct FRAC a, struct FRAC b) {
  struct FRAC c;

  c.sign = a.sign * b.sign;
  c.u = iMul(a.u, b.b);
  c.b = iMul(a.b, b.u);
  if(iComp(c.b, "0") == 0) {
    c.u = "0"; // NaN
  }
  return fReduce(c);
}

// 分数から float の値を求める（現在は未使用）
float fNum(struct FRAC a) {
  float u = 0, b = 0;
  int i;

  for(i = 0; i < a.u.length(); i++) {
    u = u * 10.0 + VAL(a.u.charAt(i));
  }
  for(i = 0; i < a.b.length(); i++) {
    b = b * 10.0 + VAL(a.b.charAt(i));
  }
  u /= b;
  u *= a.sign;
  return u;
}

///// 分数ライブラリ終わり

// 以下 RPN電卓プログラム
#include <Wire.h>
#include "U8glib.h"
U8GLIB_SSD1306_128X32 u8g(U8G_I2C_OPT_NONE); // Just for 0.91”(128*32)

// スタック周り
#define STACK_DEPTH 8
struct FRAC stack[STACK_DEPTH];

void push(struct FRAC val) {
  for(int i = STACK_DEPTH-1; i > 0; i--) {
    stack[i] = stack[i-1];
  }
  stack[0] = val;
}

struct FRAC pop(void) {
  int i;
  struct FRAC val;

  val = stack[0];
  for(int i = 0; i < STACK_DEPTH-1; i++) {
    stack[i] = stack[i+1];
  }
  // スタックの底から 0 が出てくる方式
  stack[STACK_DEPTH-1] = str2FRAC("0");
  return val;
}

void initStack(void) {
  for(int i = 0; i < STACK_DEPTH; i++) {
    stack[i] = str2FRAC("0");
  }
}

byte pin_col[] = {0, 1, 2, 3};   // PD
byte pin_row[] = {3, 2, 1, 0};   // PC

char keycheck() {
    PORTD |= _BV(pin_col[0]) | _BV(pin_col[1]) | _BV(pin_col[2]) | _BV(pin_col[3]); // set high

    for (byte i = 0; i < 4; i++) {
        PORTD &= ~_BV(pin_col[i]);  // set low

        for (byte j = 0; j < 4; j++) {
            if ((PINC & _BV(pin_row[j])) == LOW) {
                switch (4 * j + i) {
                    case  0: return '7';
                    case  1: return '8';
                    case  2: return '9';
                    case  3: return '/';

                    case  4: return '4';
                    case  5: return '5';
                    case  6: return '6';
                    case  7: return '*';

                    case  8: return '1';
                    case  9: return '2';
                    case 10: return '3';
                    case 11: return '-';

                    case 12: return '0';
                    case 13: return '.';
                    case 14: return '=';
                    case 15: return '+';
                }
            }
        }
        PORTD |= _BV(pin_col[i]);   // set high
    }
    return 0;
}

void waitRelease(int timeout) { // キーを離すまで待つ
  unsigned long time = millis();
  delay(10);
  while(keycheck()) {
    if(millis() > time + timeout) {
      break; // タイムアウト
    }
    delay(10);
  }
}

// 表示ループ
void disp(void) {
  u8g.firstPage();  
  do {
    draw();
  } while(u8g.nextPage());
}

// キー入力をつなげて文字列にする
boolean entering = false; // 文字入力中フラグ
boolean point = false; // 小数点を打ったかどうか
String ent; // 入力中の文字
void addNum(char key) {
  if(!entering) {
    ent = "";
    entering = true;
  }
  if(key == '.') {
    if(point) return; // 小数点は１個まで
    point = true;
  }
  ent = ent + String(key);
}

// 入力文字を確定してスタックに入れる
void enter(void) {
  if(entering) {
    entering = false;
    point = false;
    push(str2FRAC(ent));
    ent = "";
  }
}

// 整数を桁数制限 limit 内で E 表示する
String dispE(String s, int limit) {
  if(s.length() <= limit) {
    return s;
  }
  limit -= 2; // "En" のスペース
  if((int)s.length() - 9 > limit) // ...E10 みたいに２桁になる
    limit--;
  s = s.substring(0, limit) + "E" + String((int)s.length() - limit);
  return s;
}

#define DIGIT_LIM 12 // 表示桁数制限（表示デバイスとフォントサイズ依存）

// 分数の表示用文字列生成
String FRAC2fstr(struct FRAC a) {
  int lim = DIGIT_LIM;
  int reduce = false;
  String s = "";

  if(iComp(a.b, "0") == 0) { // 分母が 0 は NaN と表示
    return "NaN";
  }
  if(a.sign == -1) {
    lim--; // 負号 '-' のスペースを確保しておく
  }
  int blimit = max(floor(lim / 2 - 1), lim - 1 - a.u.length()); // 分母に約半分の文字数を確保
  s = "/" + dispE(a.b, blimit);
  lim -= (int)s.length(); // 分子に使用できる桁数
  s = dispE(a.u, lim) + s;

  if(a.sign < 0) { // 負号をつける
    s = "-" + s;
  }
  return s;
}

// 小数点以下の '0' や末尾の '.' を取り除く
String trimZero(String s) {
  if(s.indexOf(".") < 0) { // 小数点がない場合は処理しない
    return s;
  }
  while(s.length() > 1) {
    if(s.endsWith("0")) { // 小数点以下の '0' を削除
      s = s.substring(0, (int)s.length() - 1);
    }
    else if(s.endsWith(".")) { // 末尾の '' を削除
      s = s.substring(0, (int)s.length() - 1);
      break; // 小数点より前は処理しない
    }
    else {
      break;
    }
  }
  return s;
}

// 小数の表示用文字列生成関数　14桁に収めて表示
String FRAC2dstr(struct FRAC a) {
  int limit = DIGIT_LIM;
  String u = a.u, b = a.b, s;
  int slide = 0, lendiff, i, ptPos;

  if(iComp(b, "0") == 0) { // 分母が 0 は NaN と表示
    return "NaN";
  }
  if(iComp(u, "0") == 0) {
    return "0";
  }
  if(a.sign < 0) {
    limit--; // 負号 '-' のスペースを確保しておく
  }
  // n.xxxx の形式を前提とするためにまず桁数を揃える
  lendiff = (int)u.length() - (int)b.length();
  if(lendiff > 0) { // 分子が長い
     for(i = 0; i < lendiff; i++) {
        b = b + "0";
        slide++;
    }
  }
  else if(lendiff < 0) { //分母が長い
    for(i = 0; i < -lendiff; i++) {
      u = u + "0";
      slide--;
    }
  }
  if(iComp(u, b) < 0) { // 分子の値が小さいと 0.xxx になってしまうため
    u = u + "0"; // １桁ずらして n.xxxxの形式にする
    slide--;
  }

  // 各桁の数値を求める
  for(i = 0; i <  DIGIT_LIM - 1; i++) { // 分子を大きくする（整数除算のため）
    u = u + "0";
    slide--;
  }
  s = iDiv(u, b); // 分母分子を除算して数字の並びを求める
  ptPos = slide + (int)s.length();// 小数点の位置（文字列の先頭からの位置）
      
  // フォーマッティング
  if(0 < ptPos && ptPos < limit) { // 小数点が表示桁内に置ける場合
    u = s.substring(0, ptPos); // 小数点より前の文字列
    b = s.substring(ptPos); // 小数点より後ろの文字列
    s = u + "." + b;
    s = trimZero(s); // 小数点より後ろの 0 を取り除く
    s = s.substring(0, limit);
  }
  else if(ptPos > limit || ptPos < -limit/3) { // 絶対値が非常に大きい・小さい場合
    // x.xxxEyy の形式で表示する
    s = s.substring(0, 1) + "." + s.substring(1);
    s = trimZero(s);
    if(ptPos > 0) {
        s = s.substring(0, limit - 3); // "Eyy" の文字数を確保する
    }
    else {
        s = s.substring(0, limit - 4); // "E-yy" の文字数を確保する
    }
    s += "E" + String(ptPos - 1);
  }
  else if(ptPos == limit) { // ちょうど小数点がない場合
    s = s.substring(0, limit);
  }
  else { // 0.0000xxx の形式の場合
    for(i = 0; i < -ptPos; i++) {
        s = "0" + s;
    }
    s = "0." + s;
    s = trimZero(s);
    s = s.substring(0, limit);
  }
  if(a.sign < 0) s = "-" + s; // 負号をつける
  return s;
}

boolean fracmode = false; // 分数表示モードフラグ
String FRAC2str(struct FRAC a) { // 切り替え表示
  if(fracmode) {
    return FRAC2fstr(a);
  }
  else {
    return FRAC2dstr(a);
  }
}

// FRAC2dstr() が遅いので，演算後に表示文字列を作っておく
String dispStr[2];
void refreshStr(void) {
  dispStr[0] = FRAC2str(stack[0]);
  dispStr[1] = FRAC2str(stack[1]);
}

// 数値表示
void draw(void) {
  String s;
  
  if(entering) { // 数値の入力中
    s = ent;
    if(s.length() > DIGIT_LIM) { // 長過ぎるとき，文字の上位桁を省略表示
      s = ".." + s.substring((int)s.length() - DIGIT_LIM + 2);
    }
    u8g.setPrintPos(0, 31);
    u8g.print(s);
    u8g.setPrintPos(128 - dispStr[0].length() * 10, 15);
    u8g.print(dispStr[0]);
  }
  else {  
    u8g.setPrintPos(128 - dispStr[1].length() * 10, 15);
    u8g.print(dispStr[1]);
    u8g.setPrintPos(128 - dispStr[0].length() * 10, 31);
    u8g.print(dispStr[0]);
  }
}

// 初期化
void setup(void) {
  // u8g.setRot180(); // 表示素子を逆につけたとき
  
  u8g.setColorIndex(1);
  u8g.setFont(u8g_font_10x20r);

  CLKPR = 0x80; 
  CLKPR = 0x0;    // 分周しない

  // col
  DDRD  |= _BV(pin_col[0]) | _BV(pin_col[1]) | _BV(pin_col[2]) | _BV(pin_col[3]);     // OUTPUT
  PORTD |= _BV(pin_col[0]) | _BV(pin_col[1]) | _BV(pin_col[2]) | _BV(pin_col[3]);     // HIGH

  // row
  DDRC  &= ~(_BV(pin_row[0]) | _BV(pin_row[1]) | _BV(pin_row[2]) | _BV(pin_row[3]));  // INPUT
  PORTC |=   _BV(pin_row[0]) | _BV(pin_row[1]) | _BV(pin_row[2]) | _BV(pin_row[3]);   // PULLUP

  initStack();
  refreshStr();
  disp();
#if DEBUG
  Serial.begin( 9600 );
  Serial.println( "Start" );
#endif
}

// 長押しの時間 ミリ秒
#define LONGPRESS 500

// メインループ
void loop(void) {
  struct FRAC a;
  int longpress = false;
  
  char key = keycheck();
  if(!key)
    return;
  if(!entering) { // 長押し検出するのは入力モード以外
    unsigned long time = millis();
    waitRelease(LONGPRESS);
    if(millis() > time + LONGPRESS) {
      longpress = true;
    }
  }
  switch(key) {
  case '0':  
  case '1':  
  case '2':  
  case '3':  
  case '4':  
  case '5':  
  case '6':  
  case '7':  
  case '8':  
  case '9':  
  case '.':
    addNum(key); // 新しい数値入力
    break;
  case '=': // ENTER キー
    if(entering) { // 入力中なら確定
      enter();
    }
    else { // 確定済みのときは DUP（複製）
      a = pop();
      push(a);
      push(a);  
    }
    break;
  case '+':
    enter();
    if(longpress) {
      pop(); // // 確定してDROP（削除）
    }
    else {
      a = pop();
      stack[0] = fAdd(stack[0], a);
    }
    break;
  case '-': // 引き算または負号反転
    enter();
    if(longpress) {    // 長押しのときは符号反転
      stack[0] = fSign(stack[0]);
    }
    else {
      a = pop();
      stack[0] = fSub(stack[0], a);
    }
    break;
  case '*': // 掛け算または表示切り替え
    enter();
    if(longpress) { // 長押し時は表示切り替え
      fracmode = !fracmode;
    }
    else {
      a = pop();
      stack[0] = fMul(stack[0], a);
    }
    break;
  case '/': // 割り算または SWAP
    enter();
    if(longpress) { // 長押しのときは SWAP
      a = stack[0];
      stack[0] = stack[1];
      stack[1] = a;
    }
    else {
      a = pop();
      stack[0] = fDiv(stack[0], a);
      break;
    }
  }
  // 小数値を再計算
  if(!entering) {
    refreshStr();
  }
  disp();
  waitRelease(10000); // 事実上離すまで待ち続ける
}
